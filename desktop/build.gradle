// Desktop Application using TornadoFX, React, and Malzahar
buildscript {
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'
apply plugin: 'application'
apply plugin: 'org.junit.platform.gradle.plugin'

sourceCompatibility = '1.8'
mainClassName = 'com.lewisjmorgan.malzahar.BootstrapperKt'

ext {
    // Logging
    slf4j_version = '1.7.5'
    klogger_version = '1.4.9'
    log4j_version = '2.11.0'

    // API
    klaxon_version = '2.1.14'
    rxkotlin_version = '2.2.0'

    // Desktop
    tornadofx_version = '1.7.15'
    rxkotlinfx_version = '2.2.2'
    kodein_version = '5.0.0'

    // Testing
    spek_version = '1.1.5'
}

repositories {
    maven { url = "https://dl.bintray.com/kodein-framework/Kodein-DI" }
    maven { url = "http://dl.bintray.com/jetbrains/spek" }
}

dependencies {
    implementation project(":common-jvm")
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Logging
    implementation "org.slf4j:slf4j-api:$slf4j_version"
    implementation "io.github.microutils:kotlin-logging:$klogger_version"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"
    implementation "org.apache.logging.log4j:log4j-api:$log4j_version"

    // React
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlin_version"

    // Desktop
    implementation "no.tornado:tornadofx:$tornadofx_version"
    implementation "com.github.thomasnield:rxkotlinfx:$rxkotlinfx_version"
    implementation "org.kodein.di:kodein-di-generic-jvm:$kodein_version"
    implementation "org.kodein.di:kodein-di-conf-jvm:$kodein_version"

    // Testing
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile ("org.jetbrains.spek:spek-api:$spek_version") {
        exclude group: 'org.jetbrains.kotlin'
    }

    testRuntime ("org.jetbrains.spek:spek-junit-platform-engine:$spek_version") {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntime "org.junit.platform:junit-platform-runner:1.1.0"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': "$mainClassName"
        )
    }
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}
